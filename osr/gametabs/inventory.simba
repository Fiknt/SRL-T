(*
Inventory
=========
Methods to interact with the inventory.
*)

{$DEFINE SRL_INVENTORY_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL/osr.simba}
{$ENDIF}

const
  //Column wise and "reversed" column wise - normal pattern
  DROP_PATTERN_REGULAR: TIntegerArray = [0..27];
  DROP_PATTERN_REGULAR_R: TIntegerArray = [3,2,1,0, 7,6,5,4, 11,10,9,8, 15,14,13,12, 19,18,17,16, 23,22,21,20, 27,26,25,24];

  //Column wise snake and "reversed" version
  DROP_PATTERN_SNAKE: TIntegerArray  = [0,1,2,3, 7,6,5,4, 8,9,10,11, 15,14,13,12, 16,17,18,19, 23,22,21,20, 24,25,26,27];
  DROP_PATTERN_SNAKE_R: TIntegerArray = [3,2,1,0, 4,5,6,7, 11,10,9,8, 12,13,14,15, 19,18,17,16, 20,21,22,23, 27,26,25,24];

  //by row (and "reversed")
  DROP_PATTERN_TOPDOWN: TIntegerArray = [0,4,8,12,16,20,24,  1,5,9,13,17,21,25,  2,6,10,14,18,22,26, 3,7,11,15,19,23,27];
  DROP_PATTERN_TOPDOWN_R: TIntegerArray = [3,7,11,15,19,23,27, 2,6,10,14,18,22,26, 1,5,9,13,17,21,25,  0,4,8,12,16,20,24];

  //Spiral pattern: Starts in slot 0(top left corner of invy) and circles in a clockwise spiral
  DROP_PATTERN_SPIRAL: TIntegerArray   = [0,1,2,3, 7,11,15,19,23, 27,26,25,24, 20,16,12,8,4, 5, 6,10,14,18,22, 21,17,13,9];

type
  TRSInventory = record(TRSGameTab)
  class const
    LOW_SLOT  = 0;
    HIGH_SLOT = 27;
  var
    ItemBoxCache: record
      ClientWidth, ClientHeight: Integer;
      ClientMode: ERSClientMode;
      Boxes: TBoxArray;
    end;
  end;

function TRSInventory.GetSlotBoxes: TBoxArray;
var
  ClientW, ClientH: Integer;
begin
  GetClientDimensions(ClientW, ClientH);

  // Update cache if changes
  if (Self.ItemBoxCache.ClientMode <> RSClient.ClientMode()) or
     (Self.ItemBoxCache.ClientWidth <> ClientW) or (Self.ItemBoxCache.ClientHeight <> ClientH) then
  begin
    Self.ItemBoxCache := [
      ClientW, ClientH,
      RSClient.ClientMode(),
      Grid(4, 7, 31, 31, [11, 5], [Self.X1 + 16, Self.Y1 + 9])
    ];
  end;

  Result := Self.ItemBoxCache.Boxes;
end;

function TRSInventory.GetSlotBox(Slot: Int32): TBox;
begin
  Result := Self.GetSlotBoxes()[Slot];
end;


(*
Inventory.PointToSlot
~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.PointToSlot(P: TPoint): Int32;

Returns the inventory slot index which contains the TPoint `P`.
Returns -1 if the point does not fall in any slots.

Example
-------

  var P: TPoint;
  var Slot: Int32;

  P := Inventory.GetSlotBox(15).Middle; // Example point

  Slot := Inventory.PointToSlot(P);
  WriteLn(Slot); // 15
*)
function TRSInventory.PointToSlot(P: TPoint): Int32;
var
  I: Int32;
  Slots: TBoxArray;
begin
  Slots := Self.GetSlotBoxes();
  for I := 0 to High(Slots) do
    if Slots[I].Contains(P) then
      Exit(I);

  Result := -1;
end;

(*
Inventory.HoverSlot
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.HoverSlot(Slot: Int32): Boolean;

Moves the mouse over the slot. Slot is an Integer between 0 and 27.

Example
-------

  if Inventory.HoverSlot(1) then
    WriteLn('Mouse is now over slot #1');
*)
function TRSInventory.HoverSlot(Slot: Int32): Boolean;
begin
  Result := Self.IsOpen();
  if Result then
    Mouse.Move(Self.GetSlotBox(Slot));
end;

(*
Inventory.ClickSlot
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.ClickSlot(Slot: Int32; Option: String = ''): Boolean;

Moves the mouse and clicks on the slot. Slot is an Integer between 0 and 27.

If option is empty the slot is left clicked. Else a right click is performed
and the option is selected from the choose option menu.

Example
-------

  if Inventory.ClickSlot(1) then
    WriteLn('Left clicked slot #1');
    
  if Inventory.ClickSlot(1, 'Drop') then
    WriteLn('Right clicked and selected "Drop" on slot #1', 'Drop');  
*)
function TRSInventory.ClickSlot(Slot: Int32; Option: String = ''): Boolean;
begin
  if Self.HoverSlot(Slot) then
  begin
    if (Option <> '') then
      Result := ChooseOption.Select(Option)
    else
    begin
      Mouse.Click(MOUSE_LEFT);
      
      Result := True;
    end;
  end;
end;

function TRSInventory.IsSlotUsed(B: TBox): Boolean; overload;
begin
  // black with tolerance for when item is transparent
  Result := SRL.CountColor(CTS1(1118740, 35), B) > 0;
end;

function TRSInventory.IsSlotUsed(Slot: Int32): Boolean; overload;
begin
  Result := Self.Open() and Self.IsSlotUsed(Self.GetSlotBox(Slot));
end;

(*
Inventory.IsSlotSelected
~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.IsSlotSelected(Slot: Int32): Boolean;

Returns True if the slot is selected (white outline).

Example
-------

  if Inventory.IsSlotSelected(1) then
    WriteLn('Slot 1 is selected!');
*)
function TRSInventory.IsSlotSelected(Slot: Int32): Boolean;
var
  B: TBox;
begin
  if not Self.Open() then
    Exit(False);

  B := Self.GetSlotBox(Slot);
  B.Y1 += 8; // Skip item text

  Result := SRL.CountColor($FFFFFF, B) > 0;
end;

function TRSInventory.IsFull: Boolean;
var
  B: TBox;
begin
  if not Self.Open() then
    Exit(False);

  for B in Self.GetSlotBoxes() do
    if not Self.IsSlotUsed(B) then
      Exit(False);
  
  Result := True;
end;

(*
TRSInventory.IsSelected
~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.IsSelected(Index: Integer): Boolean;
Returns True if the index is selected (white outline).
Example
-------
  if Inventory.IsSelected(1) then
    WriteLn('Inventory index 1 is selected');
*)
function TRSInventory.IsSelected(Index: Integer): Boolean;
var
  B: TBox;
begin
  if not Self.Open() then
    Exit;

  B := Self.GetSlotBox(Index);
  B.Y1 += 8; // skip quantity

  Result := SRL.CountColor(RSColors.ITEM_BORDER_SELECTED, B) > 0;
end;


(*
TRSInventory.GetSelected
~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.GetSelected: Integer;
Returns the index of the selected item (white outline).
**-1** is returned if no item is selected.
Example
-------
  WriteLn(Inventory.GetSelected());
*)
function TRSInventory.GetSelected: Integer;
var
  I: Integer;
begin
  for I := Self.LOW_SLOT to Self.HIGH_SLOT do
    if Self.IsSelected(I) then
      Exit(I);

  Result := -1;
end;

(*
TRSInventory.ClearSelected
~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.ClearSelected: Boolean;
Unselects the currently selected item (white outline).
Example
-------
  if Inventory.ClearSelected() then
    WriteLn('No item is selected now.);
*)
function TRSInventory.ClearSelected: Boolean;
var
  Index: Integer;
begin
  if not Self.Open() then
    Exit;

  Index := Self.GetSelected();
  if (Index = -1) then
    Exit(True);

  Mouse.Click(Self.GetSlotBox(Index), MOUSE_LEFT);

  Result := WaitUntil(Self.GetSelected() = -1, 100, SRL.TruncatedGauss(1000, 2000));
end;

(*
TRSInventory.SetSelected
~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.SetSelected(Index: Integer): Boolean;
Clicks the item index to "select" it (white outline).
Example
-------
  WriteLn(Inventory.SetSelected(2));
*)
function TRSInventory.SetSelected(Index: Integer): Boolean;
var
  Selected: Integer;
begin
  if not Self.Open() then
    Exit;

  Selected := Self.GetSelected();
  if (Selected = Index) then
    Exit(True);
  if (Selected > -1) and (not Self.ClearSelected()) then
    Exit(False);

  Mouse.Click(Self.GetSlotBox(Index), MOUSE_LEFT);

  Result := WaitUntil(Self.GetSelected() = Index, 100, SRL.TruncatedGauss(1000, 2000));
end;


(*
Inventory.Use
~~~~~~~~~~~~~
.. pascal:: function TRSInventory.Use(Slot, OtherSlot: Int32): Boolean;

Selects **Slot** and uses with **OtherSlot**.

Example
-------

  Inventory.Use(1, 5); // Use item in slot 1 on Item in slot 5
*)
function TRSInventory.Use(Slot, OtherSlot: Int32): Boolean;
var
  B: TBox := Self.GetSlotBox(OtherSlot);
begin
  if B = [] then
    Exit;

  Result := Self.SetSelected(Slot);

  Wait(0, 1000, wdLeft);

  Mouse.Click(B, MOUSE_LEFT);
end;


function TRSInventory.RandomPattern: TIntegerArray;
var
  Patterns: T2DIntegerArray;
begin

  Patterns := [
    DROP_PATTERN_SNAKE, DROP_PATTERN_SPIRAL,
    DROP_PATTERN_REGULAR, DROP_PATTERN_TOPDOWN,
    DROP_PATTERN_SNAKE_R, DROP_PATTERN_TOPDOWN_R
  ];

  Result := Patterns[Random(0, High(Patterns))];
end;

function TRSInventory.ErrorPattern(Pattern: TIntegerArray=DROP_PATTERN_REGULAR; ErrorChance:Int32=5): TIntegerArray;
var i,j,h,k,tmp: Int32;
begin
  Result := Copy(Pattern);
  h := High(Pattern);
  i := 1;
  while i < h do
  begin
    if Random(100) < ErrorChance then
    begin
      case Random(0,2) of
        0:begin
            tmp := Result[i];
            for j:=i+1 to h do Result[j-1] := Result[j];
            Result[h] := tmp;
          end;
        1:begin
            Swap(Result[i+1],Result[i]);
          end;
        2:begin
            tmp := Result[i];
            k := Random(i+1,h);
            for j:=i+1 to k do Result[j-1] := Result[j];
            Result[k] := tmp;
          end;
      end;
    end;
    Inc(i);
  end;
end;

function TRSInventory.ShiftDrop(Slots: TIntegerArray): Boolean;
var
  Boxes: TBoxArray;
  I, Attempts: Int32;
  Selected: Int32;
  Circle: TCircle;
begin
  if (Length(Slots) = 0) then
    Exit(True);

  IsKeyDown(VK_SHIFT); // Clear cache

  Boxes := Self.GetSlotBoxes();

  try
    for Attempts := 1 to 5 do
    begin
      I := 0;

      while (I < Length(Slots)) do
      begin
        Selected := Self.GetSelected();
        if (Selected > -1) then
        begin
          Self.SetSelected(-1);

          Break;
        end;

        if Self.IsSlotUsed(Slots[I]) then
        begin
          if not IsKeyDown(VK_SHIFT) then
            KeyDown(VK_SHIFT);

          Mouse.Click(Self.GetSlotBox(Slots[I]), MOUSE_LEFT);

          // Spam click a little
          for 1 to SRL.TruncatedGauss(0, 3) do
          begin
            Circle.X := Mouse.Position().X;
            Circle.Y := Mouse.Position().Y;
            Circle.Radius := 5;

            Mouse.Click(Circle, MOUSE_LEFT, True);
          end;

          Wait(0, 250, wdLeft);
        end;

        Inc(I);
      end;

      if (I = Length(Slots)) then
        Exit;
    end;
  finally
    if IsKeyDown(VK_SHIFT) then
      KeyUp(VK_SHIFT);
  end;

  Result := Attempts <= 5;
end;

procedure TRSInventory.Draw(Bitmap: TMufasaBitmap); override;
begin
  if not Self.IsOpen() then
    Exit;

  inherited();

  Bitmap.DrawBoxArray(Self.GetSlotBoxes(), False, $00FFFF);
end;

function TRSInventory.Count: Int32; 
var
  I: Int32;
begin
  for I := Self.LOW_SLOT to Self.HIGH_SLOT do
    if Self.IsSlotUsed(I) then
      Inc(Result);
end;

function TRSInventory.WaitCount(Count: Int32; WaitTime: Int32; Interval: Int32 = -1): Boolean;
begin
  if (Interval = -1) then
    Interval := SRL.TruncatedGauss(50, 1500);

  Result := WaitUntil(Self.Count() = Count, Interval, WaitTime);
end;

function TRSInventory.GetSlotBoxes(Indices: TIntegerArray): TBoxArray; overload;
var
  I: Integer;
begin
  SetLength(Result, Length(Indices));
  for I := 0 to High(Indices) do
    Result[I] := Self.GetSlotBox(Indices[I]);
end;

(*
TRSInventory.GetItemBoxNearby
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.GetItemBoxNearby(Slot: Integer; Slots: TIntegerArray): TBox;
Example
-------
*)
function TRSInventory.GetSlotBoxNearby(Slot: Integer; Slots: TIntegerArray): TBox;
begin
  Slots.Remove(Slot);
  if (Length(Slots) = 0) then
    Self.Fatal('RandomSlotNearby: No slots to choose from');

  Result := SRL.RandomBoxNearby(Self.GetSlotBox(Slot).Middle, Self.GetSlotBoxes(Slots));
end;

function TRSInventory.Use(Item, OtherItem: TRSItem): Boolean; overload;
var
  Slots, OtherSlots: TIntegerArray;
  Slot: Integer;
begin
  if not Self.Open() then
    Exit;

  Self.ClearSelected();

  Slots := Self.FindItems(Item);
  OtherSlots := Self.FindItems(OtherItem);
  if (Length(Slots) = 0) or (Length(OtherSlots) = 0) then
    Exit;

  Slot := Slots[Random(Length(Slots))];

  Result := Self.SetSelected(Slot);
  if Result then
    Mouse.Click(Self.GetSlotBoxNearby(Slot, OtherSlots), MOUSE_LEFT);
end;
(*
Inventory.ShiftDrop
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSInventory.ShiftDrop(Items: TStringArray; Pattern: TIntegerArray): Boolean;

Shift drops items following the desired pattern.

Example
-------

  // Shift drop maple & willow logs in the snake pattern
  Inventory.ShiftDrop(['Maple logs', 'Willow logs'], DROP_PATTERN_SNAKE);
*)
function TRSInventory.ShiftDropItems(Items: TRSItemArray; Pattern: TIntegerArray): Boolean; overload;
var
  Slots: TIntegerArray;
begin
  Slots := Self.FindItems(Items);
  if Length(Slots) > 0 then
    Result := Self.ShiftDrop(Slots.Intersection(Pattern));
end;

function TRSInventory.ContainsItem(Item: TRSItem): Boolean; overload;
begin
  if not Self.Open() then
    Exit;

  Result := Self.ItemFinder.Contains(Item);
end;

function TRSInventory.ContainsAnyItem(Items: TRSItemArray): Boolean; overload;
begin
  if not Self.Open() then
    Exit;

  Result := Self.ItemFinder.ContainsAny(Items);
end;

function TRSInventory.ContainsItems(Items: TRSItemArray): Boolean;
begin
  if not Self.Open() then
    Exit;

  Result := Self.ItemFinder.ContainsAll(Items);
end;

function TRSInventory.FindItems(Item: TRSItem): TIntegerArray; overload;
begin
  if not Self.Open() then
    Exit;

  Result := Self.ItemFinder.IndicesOf([Item]);
end;

function TRSInventory.FindItems(Items: TRSItemArray): TIntegerArray; overload;
begin
  if not Self.Open() then
    Exit;

  Result := Self.ItemFinder.IndicesOf(Items);
end;

function TRSInventory.FindItem(Item: TRSItem): Integer;
begin
  if not Self.Open() then
    Exit;

  Result := Self.ItemFinder.IndexOf([Item]);
end;

function TRSInventory.CountItemStack(Item: TRSItem): Integer;
begin
  if not Self.Open() then
    Exit;

  Result := Self.ItemFinder.CountStack(Item);
end;

function TRSInventory.MouseItem(Item: TRSItem): Boolean;
begin
  if not Self.Open() then
    Exit;

  Result := Self.ItemFinder.Mouse(Item);
end;

function TRSInventory.ClickItem(Item: TRSItem): Boolean;
begin
  if not Self.Open() then
    Exit;

  Result := Self.ItemFinder.Click(Item);
end;

function TRSInventory.InteractItem(Item: TRSItem; Option: String): Boolean;
begin
  if not Self.Open() then
    Exit;

  Result := Self.ItemFinder.Interact(Item, Option);
end;

procedure TRSInventory.Setup(Name: String); override;
begin
  inherited;

  Self.ID := ERSGameTab.INVENTORY;
  Self.ItemFinder.GetSearchBoxesFunction := @Self.GetSlotBoxes;
end;

var
  Inventory: TRSInventory;

begin
  Inventory.Setup('Inventory');
end;
