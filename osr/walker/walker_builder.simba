{$I SRL/OSR.simba}

type
  TRSWalkerRegionBuilder = record
    Form: TForm;
    ShapeBox: TSimbaShapeBox;
  end;

procedure TRSWalkerRegionBuilder.LoadRegions;
var
  Vars: TStringArray;
  I: Integer;
  S: String;
  Box: TBox;
begin
  Vars := RTTIClassFields(RSWalkerRegions);

  for I := 0 to High(Vars) with 3 do
  begin
    if Vars[I+1] = 'TBox' then
    begin
      Box.X1 := Vars[I+2].Between('X1 = ', ',').ToInteger();
      Box.Y1 := Vars[I+2].Between('Y1 = ', ',').ToInteger();
      Box.X2 := Vars[I+2].Between('X2 = ', ',').ToInteger();
      Box.Y2 := Vars[I+2].Between('Y2 = ', '}').ToInteger();

      Self.ShapeBox.ManualAddBox(Box, 'RSWalkerRegions.' + Vars[I]);
    end;
  end;
end;

procedure TRSWalkerRegionBuilder.LoadLocations;
var
  Vars: TStringArray;
  I: Integer;
  S: String;
  Point: TPoint;
begin
  Vars := RTTIClassFields(RSWalkerLocations);

  for I := 0 to High(Vars) with 3 do
  begin
    if Vars[I+1] = 'TPoint' then
    begin
      Point.X := Vars[I+2].Between('X = ', ',').ToInteger();
      Point.Y := Vars[I+2].Between('Y = ', '}').ToInteger();

      Self.ShapeBox.ManualAddPoint(Point, 'RSWalkerLocations.' + Vars[I]);
    end;
  end;
end;

procedure TRSWalkerRegionBuilder.PrintRegions;
var
  I: Integer;
  Padding: Integer;
begin
  for I := 0 to Self.ShapeBox.ShapeCount() - 1 do
    with Self.ShapeBox.GetShape(I) do
      if IsBox then
        Padding := Max(Padding, Length(Name));

  WriteLn('type');
  WriteLn('  RSWalkerRegions = record');
  WriteLn('  class const');
  for I := 0 to Self.ShapeBox.ShapeCount() - 1 do
    with Self.ShapeBox.GetShape(I) do
    begin
      if not IsBox then
        Continue;

      Write('    ');
      Write(Name + ':');
      Write(' ' * (Padding - Length(Name)));
      Write(' TBox = [%d, %d, %d, %d];'.Format([Box.X1, Box.Y1, Box.X2, Box.Y2]));
      WriteLn();
    end;
  WriteLn('  end;');
  WriteLn('');
end;

procedure TRSWalkerRegionBuilder.PrintLocations;
var
  I: Integer;
  Padding: Integer;
begin
  for I := 0 to Self.ShapeBox.ShapeCount() - 1 do
    with Self.ShapeBox.GetShape(I) do
      if IsPoint then
        Padding := Max(Padding, Length(Name));

  WriteLn('type');
  WriteLn('  RSWalkerLocations = record');
  WriteLn('  class const');
  for I := 0 to Self.ShapeBox.ShapeCount() - 1 do
    with Self.ShapeBox.GetShape(I) do
    begin
      if not IsPoint then
        Continue;

      Write('    ');
      Write(Name + ':');
      Write(' ' * (Padding - Length(Name)));
      Write(' TPoint = [%d, %d];'.Format([Point.X, Point.Y]));
      WriteLn();
    end;
  WriteLn('  end;');
  WriteLn('');
end;

procedure TRSWalkerRegionBuilder.Run;
begin
  Self.Form.Init(nil);
  Self.Form.SetCaption('Walker Region Builder');
  Self.Form.SetPosition(poScreenCenter);
  Self.Form.SetWidth(1200);
  Self.Form.SetHeight(850);

  Self.ShapeBox.Init(Self.Form);
  Self.ShapeBox.SetParent(Self.Form);
  Self.ShapeBox.SetAlign(alClient);
  Self.ShapeBox.Panel.SetWidth(350);
  Self.ShapeBox.SetBackground(TRSWalkerMap.InternalCacheMap(SRL_WALKER_MAP));

  Self.LoadRegions();
  Self.LoadLocations();

  Self.Form.ShowModal();

  ClearDebug();

  Self.PrintRegions();
  Self.PrintLocations();

  Self.Form.Free();
end;

var
  WalkerRegionBuilder: TRSWalkerRegionBuilder;

begin
  Sync(@WalkerRegionBuilder.Run);
end.
