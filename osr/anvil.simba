{$DEFINE SRL_ANVIL_INCLUDED}
{$IFNDEF SRL_OSR}
  {$I SRL/osr.simba}
{$ENDIF}

(*
Anvil
=====
Anvil methods, used for smithing items.
*)

type
  TRSAnvil = record(TRSInterface)
  class const
    ELEMENT_QUANTITY_1: TRSInterfaceElement = [453, 36, 36, 36];
    ELEMENT_QUANTITY_2: TRSInterfaceElement = [453, 81, 36, 36];
    ELEMENT_QUANTITY_3: TRSInterfaceElement = [453, 126, 36, 36];
    ELEMENT_QUANTITY_4: TRSInterfaceElement = [453, 171, 36, 36];
    ELEMENT_QUANTITY_5: TRSInterfaceElement = [453, 216, 36, 36];
    ELEMENT_ITEM_AREA:  TRSInterfaceElement = [10, 36, 385, 275];

    QUANTITY_ALL: Integer = -1;
  end;

function TRSAnvil.FindItemBoundaries: TBoxArray;
var
  B: TBox;
begin
  B := Self.ElementFinder.Bounds(Self.ELEMENT_ITEM_AREA);

  Result := B.Partition(5,5);
end;

function TRSAnvil.SetQuantity(Amount: Integer): Boolean;
var
  Text: String;
  Element: TRSInterfaceElement;
begin
  // Already selected?
  for Element in [Self.ELEMENT_QUANTITY_1, Self.ELEMENT_QUANTITY_2, Self.ELEMENT_QUANTITY_3, Self.ELEMENT_QUANTITY_4, Self.ELEMENT_QUANTITY_5] do
  begin
    Text := Self.ElementFinder.ReadText(Element, [RSColors.TEXT_WHITE], RS_FONT_PLAIN_11);
    if (Text = ToStr(Amount)) or ((Amount = Self.QUANTITY_ALL) and (Text in ['AII', 'All'])) then
      Exit(True);
  end;

  // Select
  for Element in [Self.ELEMENT_QUANTITY_1, Self.ELEMENT_QUANTITY_2, Self.ELEMENT_QUANTITY_3, Self.ELEMENT_QUANTITY_4, Self.ELEMENT_QUANTITY_5] do
  begin
    Text := Self.ElementFinder.ReadText(Element, [RSColors.TEXT_ORANGE], RS_FONT_PLAIN_11);
    if (Text = ToStr(Amount)) or ((Amount = Self.QUANTITY_ALL) and (Text in ['AII', 'All'])) then
      Self.ElementFinder.Click(Element);
  end;

  // Must be custom
  for Element in [Self.ELEMENT_QUANTITY_1, Self.ELEMENT_QUANTITY_2, Self.ELEMENT_QUANTITY_3, Self.ELEMENT_QUANTITY_4, Self.ELEMENT_QUANTITY_5] do
    if Self.ElementFinder.HasText(Element, [RSColors.TEXT_ORANGE], 'X', RS_FONT_PLAIN_11) then
    begin
      Self.ElementFinder.Click(Element);

      Result := Chat.AnswerQuery('Enter amount', ToString(Amount), Random(2500, 3500));
      Exit;
    end;
end;

(*
Anvil.IsOpen
~~~~~~~~~~~~
.. pascal:: function TRSAnvil.IsOpen: Boolean;

Returns true if the Anvil screen is open.
*)
function TRSAnvil.IsOpen: Boolean; overload;
begin
  Result := Self.IsTitle('would like to') or Self.IsTitle('make?');
end;

function TRSAnvil.IsOpen(WaitTime: Integer; Interval: Integer = -1): Boolean; overload;
begin
  if (Interval = -1) then
    Interval := RandomLeft(50, 1500);

  Result := WaitUntil(Self.IsOpen(), Interval, WaitTime);
end;

procedure TRSAnvil.Setup(Name: String); override;
begin
  inherited;

  Self.ItemFinder.Name := Name + '.ItemFinder';
  Self.ItemFinder.GetSearchBoxesFunction := @Self.FindItemBoundaries;

  with Self.BoundsFinder.Alignments[ERSClientMode.FIXED] do
  begin
    Left   := [@InterfaceArea.X1];
    Right  := [@InterfaceArea.X2];
    Top    := [@InterfaceArea.Y1];
    Bottom := [@InterfaceArea.Y2];

    Center.MaxWidth := 500;
    Center.MaxHeight := 320;
  end;

  with Self.BoundsFinder.Alignments[ERSClientMode.RESIZABLE_CLASSIC] do
  begin
    Left   := [@InterfaceArea.X1];
    Right  := [@InterfaceArea.X2];
    Top    := [@InterfaceArea.Y1, -1];
    Bottom := [@InterfaceArea.Y2, -1];

    Center.MaxWidth := 500;
    Center.MaxHeight := 320;
  end;

  // Same as above
  Self.BoundsFinder.Alignments[ERSClientMode.RESIZABLE_MODERN] := Self.BoundsFinder.Alignments[ERSClientMode.RESIZABLE_CLASSIC];
end;

var
  Anvil: TRSAnvil;

begin
  Anvil.Setup('Anvil');
end;
